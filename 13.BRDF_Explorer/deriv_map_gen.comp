#version 450 core

layout (local_size_x = 16, local_size_y = 16) in;

layout (binding = 7) uniform sampler2D bumpMapSampler;
layout (binding = 0, rg8_snorm) uniform image2D derivativeMapImage;

layout (location = 0) uniform float uHeightScaleFactor;

shared float smem[324];//18*18

int getAddr(in ivec2 threadID)
{
	return 18*(threadID.y+1) + threadID.x+1;
}

void main()
{
	const ivec2 G_IDX = ivec2(gl_GlobalInvocationID.xy);
	
	const ivec2 bumpMapSz = textureSize(bumpMapSampler, 0);

	const ivec2 LC_IDX = ivec2(gl_LocalInvocationID.xy);
	smem[getAddr(LC_IDX)] = texelFetch(bumpMapSampler, G_IDX, 0).x * uHeightScaleFactor;
	if (LC_IDX.x == 0 || LC_IDX.x == 15) // TODO how not to use if-statements??
	{
		ivec2 offset = ivec2(mix(ivec2(1, 0), ivec2(-1, 0), LC_IDX.x == 0));
		smem[getAddr(LC_IDX+offset)] = texelFetch(bumpMapSampler, G_IDX+offset, 0).x * uHeightScaleFactor;
	}
	if (LC_IDX.y == 0 || LC_IDX.y == 15)
	{
		ivec2 offset = ivec2(mix(ivec2(0, 1), ivec2(0, -1), LC_IDX.y == 0));
		smem[getAddr(LC_IDX+offset)] = texelFetch(bumpMapSampler, G_IDX+offset, 0).x * uHeightScaleFactor;
	}
	
	barrier(); // AFAIK execution barrier implies memory barrier
	
	vec2 d = vec2(
		smem[getAddr(LC_IDX+ivec2(1, 0))] - smem[getAddr(LC_IDX-ivec2(1, 0))],
		smem[getAddr(LC_IDX+ivec2(0, 1))] - smem[getAddr(LC_IDX-ivec2(0, 1))]
	) * 0.5 * vec2(bumpMapSz);
	
	if (all(lessThan(G_IDX, bumpMapSz)))
		imageStore(derivativeMapImage, G_IDX, vec4(d, 0.0, 0.0));
}