#version 430 core
#include "raytraceCommon.h"
layout(local_size_x = WORKGROUP_DIM, local_size_y = WORKGROUP_DIM) in;

layout(set = 0, binding = 0, row_major) uniform StaticViewData
{
	StaticViewData_t staticViewData;
};
layout(set = 0, binding = 1) uniform usampler2DArray colorSamples;
layout(set = 0, binding = 2) uniform sampler2DArray albedoSamples;
layout(set = 0, binding = 3) uniform usampler2DArray normalSamples;
layout(set = 0, binding = 4, rgba16f) restrict uniform image2D framebuffer;
layout(set = 0, binding = 5, r32ui) restrict uniform uimage2D albedo;
layout(set = 0, binding = 6, rgba16f) restrict uniform image2D normals;

#include <nbl/builtin/glsl/format/decode.glsl>
#include <nbl/builtin/glsl/format/encode.glsl>
vec3 fetchAccumulation(in uvec2 coord, in uint subsample)
{
	const uvec2 data = texelFetch(colorSamples,ivec3(coord,subsample),0).rg;
	return nbl_glsl_decodeRGB19E7(data);
}

void main()
{
	const ivec2 pixelCoord = ivec2(gl_GlobalInvocationID.xy);
	if (all(lessThan(pixelCoord,staticViewData.imageDimensions)))
	{
		vec3 acc = fetchAccumulation(gl_GlobalInvocationID.xy,0u);
		vec3 alb = texelFetch(albedoSamples,ivec3(pixelCoord,0),0).rgb;
		vec3 nml = nbl_glsl_decodeRGB10A2_SNORM(texelFetch(normalSamples,ivec3(pixelCoord,0),0).r).xyz;
		for (uint i=1u; i<staticViewData.samplesPerPixelPerDispatch; i++)
		{
			acc += fetchAccumulation(gl_GlobalInvocationID.xy,i);
			alb += texelFetch(albedoSamples,ivec3(pixelCoord,i),0).rgb;
			nml += nbl_glsl_decodeRGB10A2_SNORM(texelFetch(normalSamples,ivec3(pixelCoord,i),0).r).xyz;
		}
		acc /= float(staticViewData.samplesPerPixelPerDispatch);
		alb /= float(staticViewData.samplesPerPixelPerDispatch);
		nml /= float(staticViewData.samplesPerPixelPerDispatch);

		// transform normal

		imageStore(framebuffer,pixelCoord,vec4(acc,1.f));
		imageStore(albedo,pixelCoord,uvec4(nbl_glsl_encodeRGB10A2_UNORM(vec4(alb,1.0)),0u,0u,0u));
		imageStore(normals,pixelCoord,vec4(nml,1.f));
	}
	// TODO: record autoexposure histogram
}